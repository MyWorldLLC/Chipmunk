/*
 * Copyright (C) 2020 MyWorld, LLC
 * All rights reserved.
 *
 * This file is part of Chipmunk.
 *
 * Chipmunk is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Chipmunk is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Chipmunk.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id "org.panteleyev.jpackageplugin" version "1.5.0"
}

apply plugin: 'java'


group = 'myworld'
version = rootProject.ext.buildVersion

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':Lang')
    implementation project(':Package')
    implementation 'info.picocli:picocli:4.5.2'
    annotationProcessor 'info.picocli:picocli-codegen:4.5.2'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    baseName 'Chipmunk-CLI'
    manifest {
        attributes (
                'Application-Name' : 'Chipmunk CLI',
                'Main-Class' : 'chipmunk.cli.ChipmunkCLI'
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy(DuplicatesStrategy.WARN)
}

clean {
    delete 'build'
    delete 'dist'
}

task run(type: JavaExec){
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
    mainClass = 'chipmunk.cli.ChipmunkCLI'
    classpath = sourceSets.main.runtimeClasspath
}

def sanitizedVersion(){
    def os = org.gradle.nativeplatform.platform.internal.DefaultNativePlatform.currentOperatingSystem
    def sanitizedVersion = version.replaceFirst('\\D+', '')
    if(os.isWindows() && sanitizedVersion.indexOf('-') != -1) {
        sanitizedVersion = sanitizedVersion.substring(0, sanitizedVersion.indexOf('-'))
    }
    sanitizedVersion
}

jpackage {
    input = 'build/libs'
    destination = 'build/installer'
    appName = 'chipmunk'

    // Remove leading non-digits from version string, as some platforms (such as Debian) do not permit this
    appVersion = sanitizedVersion()

    mainClass = 'chipmunk.cli.ChipmunkCLI'
    mainJar = "${jar.archiveFile.get().asFile.getName()}"
    verbose = "verbose"

    resourceDir = 'jpackage'
    temp = 'build/jpackage-tmp'

    winConsole = true
}
tasks.jpackage.dependsOn = [build]