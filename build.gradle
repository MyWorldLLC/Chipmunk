
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
    }
}

plugins {
	id 'me.champeau.gradle.jmh' version '0.5.0'
	id 'net.ltgt.apt' version '0.18'
	id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = 1.9
targetCompatibility = 1.9

group = 'myworld'
version = '1.0.0-SNAPSHOT'

repositories {
	mavenCentral()
}

dependencies {
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    jmh 'org.openjdk.jmh:jmh-core:1.19'
}

sourceSets {
	main {
	  java {
		srcDir 'src/main/java'
	  }
	}
	
	profiling {
		java {
			srcDir 'src/profiling/java'
			compileClasspath += main.compileClasspath + main.output
			runtimeClasspath += main.output
		}
		resources {
			srcDir 'src/profiling/resources'
		}
	}
	
	jmh {
		java {
			srcDir 'src/jmh/java'
		}
		resources {
			srcDir 'src/jmh/resources'
		}
	}
}

jar {
	baseName 'Chipmunk'
}

publishing {
	publications {
		gpr(MavenPublication) {
			groupId = "${project.group}"
			artifactId = 'chipmunk'
			version = "${project.version}"

			from components.java
		}
	}

	repositories {
		mavenLocal()
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/MyWorldLLC/Chipmunk")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
	}
}

asciidoctor {
	sourceDir 'src/doc/asciidoc'
}

clean {
	delete 'build'
	delete 'dist'
}

jmh {
	fork = 1
	jvmArgs = ['-XX:+UnlockExperimentalVMOptions', '-XX:+EnableJVMCI', '-XX:+UseJVMCICompiler'] // Ensure Graal is enabled
}

task runProfiler(type:JavaExec) {
   main = 'chipmunk.profiling.ChipmunkProfiler'
   classpath = sourceSets.profiling.runtimeClasspath + sourceSets.profiling.compileClasspath
}
