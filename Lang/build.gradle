
/*
 * Copyright (C) 2020 MyWorld, LLC
 * All rights reserved.
 *
 * This file is part of Chipmunk.
 *
 * Chipmunk is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Chipmunk is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Chipmunk.  If not, see <https://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.8'
    }
}

plugins {
	id 'me.champeau.gradle.jmh' version '0.5.2'
	id 'net.ltgt.apt' version '0.18'
	id 'maven-publish'
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.convert'

sourceCompatibility = JavaVersion.VERSION_14
targetCompatibility = JavaVersion.VERSION_14

group = 'myworld'
version = findProperty('release') ? findProperty('release') : rootProject.ext.snapshotVersion

repositories {
	mavenCentral()
}

dependencies {

	compile'org.ow2.asm:asm:9.0'

    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    jmh 'org.openjdk.jmh:jmh-core:1.26'
}

sourceSets {
	main {
	  java {
		srcDir 'src/main/java'
	  }
	}
	
	profiling {
		java {
			srcDir 'src/profiling/java'
			compileClasspath += main.compileClasspath + main.output
			runtimeClasspath += main.output
		}
		resources {
			srcDir 'src/profiling/resources'
		}
	}
	
	jmh {
		java {
			srcDir 'src/jmh/java'
		}
		resources {
			srcDir 'src/jmh/resources'
		}
	}
}

jar {
	baseName 'Chipmunk'
}

publishing {
	publications {
		gpr(MavenPublication) {
			groupId = "${project.group}"
			artifactId = 'chipmunk'
			version = "${project.version}"

			from components.java
		}
	}

	repositories {
		mavenLocal()
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/MyWorldLLC/Chipmunk")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
				password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
			}
		}
	}
}

asciidoctor {
	sourceDir 'src/doc/asciidoc'
}

clean {
	delete 'build'
	delete 'dist'
}

jmh {
	fork = 1
	//jvmArgs = ['-XX:+UnlockExperimentalVMOptions', '-XX:+EnableJVMCI', '-XX:+UseJVMCICompiler'] // Ensure Graal is enabled
}

task runProfiler(type:JavaExec) {
    main = 'chipmunk.profiling.ChipmunkProfiler'
	//jvmArgs = ['-XX:+UnlockExperimentalVMOptions', '-XX:+EnableJVMCI', '-XX:+UseJVMCICompiler']
    classpath = sourceSets.profiling.runtimeClasspath + sourceSets.profiling.compileClasspath
}
